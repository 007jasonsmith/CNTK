# configFile=$(SolutionDir)Examples/Text/PennTreebank/Config/S2SAutoEncoder.cntk  RunDir=$(SolutionDir)Examples/Text/PennTreebank/_run  RootDir=$(SolutionDir)Examples/Text/PennTreebank/_run  DataDir=$(SolutionDir)Examples/Text/PennTreebank/Data  ConfigDir=$(SolutionDir)Examples/Text/PennTreebank/Config  stderr=$(SolutionDir)Examples/Text/PennTreebank/_run/S2SAutoEncoder.log  DeviceId=-1  makeMode=false
####################
# WORK IN PROGRESS #
# WORK IN PROGRESS #
# WORK IN PROGRESS #
####################

# Command line to run in debugger:
# configFile=$(SolutionDir)Examples/Text/PennTreebank/Config/S2SAutoEncoder.cntk  RunDir=$(SolutionDir)Examples/Text/PennTreebank/_run  RootDir=$(SolutionDir)Examples/Text/PennTreebank/_run  DataDir=$(SolutionDir)Examples/Text/PennTreebank/Data  ConfigDir=$(SolutionDir)Examples/Text/PennTreebank/Config  stderr=$(SolutionDir)Examples/Text/PennTreebank/_run/S2SAutoEncoder.log  train=[SGD=[maxEpochs=1]]  confVocabSize=1000  DeviceId=-1  makeMode=false
# Append this for small set:
# train=[epochSize=2048]]  trainFile=ptb.small.train.txt  validFile=ptb.small.valid.txt testFile=ptb.small.test.txt

# It implements a sequence-to-sequence based auto-encoder.
# It encodes an entire sentence into a flat vector, and tries to regenerate it.
# Meant to be useful mainly understanding how to do sequence-to-sequence in CNTK.

# Parameters can be overwritten on the command line
# for example: cntk configFile=myConfigFile RootDir=../.. 
# For running from Visual Studio add
# currentDirectory=$(SolutionDir)/<path to corresponding data folder> 
RootDir = ".."

ConfigDir = "$RootDir$/Config"
DataDir   = "$RootDir$/Data"
CacheDir  = "$RootDir$/Data/cache"
OutputDir = "$RootDir$/Output"
ModelDir  = "$OutputDir$/Models"

# deviceId=-1 for CPU, >=0 for GPU devices, "auto" chooses the best GPU, or CPU if no usable GPU is available
deviceId = "auto"

#command = writeWordAndClassInfo:train:test:write
command = write

precision  = "float"
traceLevel = 1
modelPath  = "$ModelDir$/S2SAutoEncoder.dnn"

# uncomment the following line to write logs to a file
#stderr=$OutputDir$/rnnOutput

#numCPUThreads = 1

confVocabSize = 10000
confClassSize = 50
useStabilizer = true

trainFile = "ptb.train.txt"
#trainFile = "ptb.small.train.txt"
validFile = "ptb.valid.txt"
#validFile = "ptb.small.valid.txt"
testFile  = "ptb.test.txt"

#######################################
#  network definition                 #
#######################################

BrainScriptNetworkBuilder = (new ComputationNetwork [

    # import general config options from outside config values
    vocabDim = $confVocabSize$
    nbrClass = $confClassSize$

    useStabilizer = $useStabilizer$
    useEncoder = true                 // if false, this becomes a regular RNN

    # import some namespaces
    Parameters = BS.Parameters
    Constants  = BS.Constants
    Sequences  = BS.Sequences
    Loop       = BS.Loop
    Boolean    = BS.Boolean
    RecurrentLSTMP = BS.RNNs.RecurrentLSTMP

    # define an LSTM with a per-sequence initialization value
    # TODO: Not currently used. Move to BS library once tested.
    RecurrentLSTMPWithInitValue (inputDim, outputDim, cellDim, x, initValue, enableSelfStabilization=false) =
    [
        prevState =  // Loop.Previous (lstmState). BS can't apply Previous() to dictionaries, so expand it manually
        [
            h = Loop.Previous (lstmState.h);                     // hidden state(t-1)
            c = Loop.Previous (lstmState.c);                     // cell(t-1)
        ]
        # resettable LSTM function
        lstmState =
        [
            // apply the LSTM function to the input state; for first frame, we will ignore the output
            enableSelfStabilization1 = enableSelfStabilization // TODO: BS syntax needs to allow to say ^.enableSelfStabilization
            lstmState1 = LSTMP (inputDim, outputDim, cellDim, x, prevState, enableSelfStabilization=enableSelfStabilization1)

            // the actual LSTM state (incl. its output) gets overwritten in the first frame by the initValue
            isFirst = Loop.IsFirst (x)
            h = Boolean.If (isFirst, initValue, lstmState1.h); // hidden state(t-1)
            c = Boolean.If (isFirst, initValue, lstmState1.c); // cell(t-1)
        ]
    ].lstmState.h // that's the value we return

    embeddingDim = 300
    hiddenDim    = 200

    encoderDims[i:0..0] = hiddenDim # this defines the number of hidden layers in each
    decoderDims[i:0..0] = hiddenDim # both are one LSTM layer only for now

    # inputs
    #input = SparseInput(vocabDim, tag='feature');  # BUGBUG: Slice() not working for sparse, need to extend TensorView
    input = Input(vocabDim, tag='feature');
    # for an auto-encoder, both are the same
    labels = input

    # embedding
    # BUGBUG: Stabilizer must be part of E, not input, so that we only have one that is shared between encoder and decoder
    E = Parameters.WeightParam (vocabDim, embeddingDim) # note: this is assumed to be applied transposed, hence the swapped dimensions
    Embed (x) = TransposeTimes (E, Parameters.Stabilize (x, enabled=useStabilizer)) # embeddings are linear, so better stabilize. We really should use BatchNorm.
    #E = Parameters.WeightParam (embeddingDim, vocabDim) # note: this is assumed to be applied transposed, hence the swapped dimensions
    #Embed (x) = new ComputationNode [ operation = 'LookupTable' ; inputs = (E : Parameters.Stabilize (x, enabled=useStabilizer)) ; tag = '' ]

    inputEmbedded  = Embed (input)
    labelsEmbedded = Embed (labels)

    # encoder (processes user input)
    encoderOutputLayer = Length (encoderDims)-1
    encoder[i:0..encoderOutputLayer] =
        RecurrentLSTMP(if i == 0 then embeddingDim else encoderDims[i-1],
                       encoderDims[i], encoderDims[i],
                       if i == 0 then inputEmbedded else encoder[i-1],
                       enableSelfStabilization=useStabilizer)
    encoderOutput = encoder[encoderOutputLayer]

    # that last frame should be fed as an additional input to every decoder step
    # (This is the NYU model, not the Google model where the thought vector is the initial state.)
    thoughtVector =
    [
        x = encoderOutput
        result = Boolean.If (Loop.IsLast (x),      // if last entry
                 /*then*/ x,                       // then copy that
                 /*else*/ FutureValue (0, result)) // else just propagate to the front
    ].result
    thoughtVectorDim = encoderDims[encoderOutputLayer]

    # decoder
    # The decoder starts with hidden state 0
    # and takes as input (thoughtVector; previous word).
    delayedDecoderFeedback = Loop.Previous (decoderFeedback)
    decoderInputDim = if useEncoder then           thoughtVectorDim        + embeddingDim  else           embeddingDim
    decoderInput    = if useEncoder then RowStack (thoughtVector : delayedDecoderFeedback) else delayedDecoderFeedback
    decoderOutputLayer = Length (decoderDims)-1
    decoder[i:0..decoderOutputLayer] =
        if i == 0
        then RecurrentLSTMP (decoderInputDim, decoderDims[i], decoderDims[i],
                             decoderInput,
                             enableSelfStabilization=useStabilizer)
        else RecurrentLSTMP (decoderDims[i-1], decoderDims[i], decoderDims[i],
                             decoder[i-1],
                             enableSelfStabilization=useStabilizer)
    decoderDim = decoderDims[decoderOutputLayer]
    decoderOutput = decoder[decoderOutputLayer]

    # and add a softmax layer on top
    W(x) = Parameters.WeightParam (vocabDim, decoderDim) * Parameters.Stabilize (x, enabled=useStabilizer)
    B = Parameters.BiasParam (vocabDim)

    z = W(decoderOutput) + B;  // top-level input to Softmax

    decoderOutputEmbedded = Embed (Hardmax (z))

    # decoder feedback differs between training and test
    isTraining = EnvironmentInput ('isTraining', tag='evaluation')
    #decoderFeedback = Boolean.If (isTraining, labelsEmbedded, decoderOutputEmbedded)
    decoderFeedback = labelsEmbedded

    # exclude the first token, which is sentence start. Don't want to train on that.
    CastAs (type, data) = Sequences.Scatter (Constants.OnesLike (type), data)

    SkipFirst (x) = Sequences.Skip (1, x)
    z1      =             SkipFirst (z)
    labels1 = CastAs (z1, SkipFirst (labels))

    # training criteria
    ce  = CrossEntropyWithSoftmax(labels1, z1, tag='criterion')   // this is the training objective
    wer = ErrorPrediction        (labels1, z1, tag='evaluation')  // this also gets tracked

    #indexTestVals = Plus (decoderOutput, BS.Constants.Zero, tag='evaluation')
    #indexTest = Slice (0, 1, indexTestVals)
    #index = Where (RectifiedLinear (indexTest), tag='evaluation'); // for testing: this thresholds all negative numbers to 0=false, keeping positive as !=0=true
    #packedIndex = PackedIndex (indexTest, index, tag='evaluation')
    #filtered = GatherPacked (packedIndex, indexTestVals, tag='evaluation')
    #unfiltered = ScatterPacked (indexTest, packedIndex, filtered, tag='evaluation')
])

#######################################
# shared reader definition            #
#######################################

reader = [
    file = "$DataDir$/$trainFile$"
    #randomize = "auto" # gets ignored

    readerType = LMSequenceReader
    mode = "softmax"
    nbruttsineachrecurrentiter = 0      # 0 means auto-fill given minibatch size
    cacheBlockSize = 100000000          # read block size. This value is large enough to load entire corpus at once

    # word class info
    wordclass = "$ModelDir$/vocab.txt"

    #### write definition
    # if writerType is set, we will cache to a binary file
    # if the binary file exists, we will use it instead of parsing this file
    #writerType = BinaryReader
    wfile = $CacheDir$\sequenceSentence.bin
    # if calculated size would be bigger, that is used instead
    wsize = 256
    #wrecords - number of records we should allocate space for in the file
    # files cannot be expanded, so this should be large enough. If known modify this element in config before creating file
    wrecords = 1000
    #windowSize - number of records we should include in BinaryWriter window
    windowSize = 10000

    # additional features sections
    # For input labels, we need both 'features' and the first labels section (called 'inputLabelsDef' below)
    input = [
        dim = 0     # no (explicit) labels   ...labelDim correct??
        ### write definition
        sectionType = "data"
    ]
    # labels sections
    # TODO: seems we must specify two labels (in and out), but labelType = "none" is allowed
    # labels sections  --this is required, but our labels are extracted from the inLabels
    inputLabelsDef = [ # BUGBUG: Make sure that this section name comes before the dummy output labels alphabetically
        dim = 1

        # vocabulary size
        labelType = "category"
        labelDim = "$confVocabSize$"
        labelMappingFile = "$ModelDir$/vocab.wl"
        beginSequence = "</s>"
        endSequence   = "</s>"

        #### Write definition ####
        # sizeof(unsigned) which is the label index type
        elementSize=4
        sectionType=labels
        mapping = [
          #redefine number of records for this section, since we don't need to save it for each data record
          wrecords=11
          #variable size so use an average string size
          elementSize=10
          sectionType=labelMapping
        ]
        category = [
          dim=11
          #elementSize=sizeof(ElemType) is default
          sectionType=categoryLabels
        ]
    ]
    outputDummy = [
        labelType = "none"
    ]
]

cvReader = [
    file = "$DataDir$/$validFile$"
    #randomize = "none" # gets ignored

    # everything below here is duplicated from 'reader'
    readerType = LMSequenceReader
    mode = "softmax"
    nbruttsineachrecurrentiter = 0      # 0 means auto-fill given minibatch size
    cacheBlockSize = 100000000          # read block size. This value is large enough to load entire corpus at once

    # word class info
    wordclass = "$ModelDir$/vocab.txt"

    #### write definition
    # if writerType is set, we will cache to a binary file
    # if the binary file exists, we will use it instead of parsing this file
    #writerType = BinaryReader
    wfile = $CacheDir$\sequenceSentence.bin
    # if calculated size would be bigger, that is used instead
    wsize = 256
    #wrecords - number of records we should allocate space for in the file
    # files cannot be expanded, so this should be large enough. If known modify this element in config before creating file
    wrecords = 1000
    #windowSize - number of records we should include in BinaryWriter window
    windowSize = 10000

    # additional features sections
    # For input labels, we need both 'features' and the first labels section (called 'inputLabelsDef' below)
    input = [
        dim = 0     # no (explicit) labels   ...labelDim correct??
        ### write definition
        sectionType = "data"
    ]
    # labels sections
    # TODO: seems we must specify two labels (in and out), but labelType = "none" is allowed
    # labels sections  --this is required, but our labels are extracted from the inLabels
    inputLabelsDef = [ # BUGBUG: Make sure that this section name comes before the dummy output labels alphabetically
        dim = 1

        # vocabulary size
        labelType = "category"
        labelDim = "$confVocabSize$"
        labelMappingFile = "$ModelDir$/vocab.wl"
        beginSequence = "</s>"
        endSequence   = "</s>"

        #### Write definition ####
        # sizeof(unsigned) which is the label index type
        elementSize=4
        sectionType=labels
        mapping = [
          #redefine number of records for this section, since we don't need to save it for each data record
          wrecords=11
          #variable size so use an average string size
          elementSize=10
          sectionType=labelMapping
        ]
        category = [
          dim=11
          #elementSize=sizeof(ElemType) is default
          sectionType=categoryLabels
        ]
    ]
    outputDummy = [
        labelType = "none"
    ]
]

#######################################
#  PREPARATION CONFIG                 #
#######################################

writeWordAndClassInfo = [
    action = "writeWordAndClass"
    inputFile = "$DataDir$/$trainFile$"
    beginSequence = "</s>"
    endSequence   = "</s>"
    outputMappingFile = "$ModelDir$/vocab.wl"
    outputVocabFile = "$ModelDir$/vocab.txt"
    outputWord2Cls  = "$ModelDir$/word2cls.txt"
    outputCls2Index = "$ModelDir$/cls2idx.txt"
    vocabSize = "$confVocabSize$"
    nbrClass = "$confClassSize$"
    cutoff = 0
    printValues = true
]

#######################################
#  TRAINING CONFIG                    #
#######################################

train = [
    action = "train"
    traceLevel = 1
    epochSize = 0               # (for quick tests, this can be overridden with something small)

    #BrainScriptNetworkBuilder is defined in outer scope

    SGD = [
        minibatchSize = 128*2:256:512
        learningRatesPerSample = 0.01
        #momentumPerMB = 0
        momentumAsTimeConstant = 2500
        gradientClippingWithTruncation = true   # TODO: clip and truncate? What is the difference?
        clippingThresholdPerSample = 15.0
        maxEpochs = 16
        numMBsToShowResult = 100
        gradUpdateType = "none" # FSAdaGrad?
        loadBestModel = true

        # tracing (enable these for debugging)
        #traceNodeNamesReal = labelsEmbedded:decoderInput:"decoder[0].lstmState._privateInnards.ht":z.Plus_left.Times_right.result:z:ce
        #traceNodeNamesReal = labelsEmbedded:decoderInput:z:ce
        #traceNodeNamesReal = thoughtVector.result:zMask:z:ce:wer:indexTestVals:index:packedIndex:filtered:unfiltered:isTraining
        #traceNodeNamesCategory = input

        dropoutRate = 0.0

        # settings for Auto Adjust Learning Rate
        AutoAdjust = [
            autoAdjustLR = "adjustAfterEpoch"
            reduceLearnRateIfImproveLessThan = 0.001
            continueReduce = false
            increaseLearnRateIfImproveMoreThan = 1000000000
            learnRateDecreaseFactor = 0.5
            learnRateIncreaseFactor = 1.382
            numMiniBatch4LRSearch = 100
            numPrevLearnRates = 5
            numBestSearchEpoch = 1
        ]
    ]
]

#######################################
#  TEST CONFIG                        #
#######################################

test = [
    action = "eval"

    # correspond to the number of words/characteres to train in a minibatch
    minibatchSize = 8192                # choose as large as memory allows for maximum GPU concurrency
    # need to be small since models are updated for each minibatch
    traceLevel = 1
    epochSize = 0

    reader = [
        file = "$DataDir$/$testFile$"
        #randomize = "none" # gets ignored
    
        # everything below here is duplicated from 'reader'
        readerType = LMSequenceReader
        mode = "softmax"
        nbruttsineachrecurrentiter = 0      # 0 means auto-fill given minibatch size
        cacheBlockSize = 100000000          # read block size. This value is large enough to load entire corpus at once
    
        # word class info
        wordclass = "$ModelDir$/vocab.txt"
    
        #### write definition
        # if writerType is set, we will cache to a binary file
        # if the binary file exists, we will use it instead of parsing this file
        #writerType = BinaryReader
        wfile = $CacheDir$\sequenceSentence.bin
        # if calculated size would be bigger, that is used instead
        wsize = 256
        #wrecords - number of records we should allocate space for in the file
        # files cannot be expanded, so this should be large enough. If known modify this element in config before creating file
        wrecords = 1000
        #windowSize - number of records we should include in BinaryWriter window
        windowSize = 10000
    
        # additional features sections
        # For input labels, we need both 'features' and the first labels section (called 'inputLabelsDef' below)
        input = [
            dim = 0     # no (explicit) labels   ...labelDim correct??
            ### write definition
            sectionType = "data"
        ]
        # labels sections
        # TODO: seems we must specify two labels (in and out), but labelType = "none" is allowed
        # labels sections  --this is required, but our labels are extracted from the inLabels
        inputLabelsDef = [ # BUGBUG: Make sure that this section name comes before the dummy output labels alphabetically
            dim = 1
    
            # vocabulary size
            labelType = "category"
            labelDim = "$confVocabSize$"
            labelMappingFile = "$ModelDir$/vocab.wl"
            beginSequence = "</s>"
            endSequence   = "</s>"
    
            #### Write definition ####
            # sizeof(unsigned) which is the label index type
            elementSize=4
            sectionType=labels
            mapping = [
              #redefine number of records for this section, since we don't need to save it for each data record
              wrecords=11
              #variable size so use an average string size
              elementSize=10
              sectionType=labelMapping
            ]
            category = [
              dim=11
              #elementSize=sizeof(ElemType) is default
              sectionType=categoryLabels
            ]
        ]
        outputDummy = [
            labelType = "none"
        ]
    ]
]

#######################################
#  WRITE CONFIG                       #
#######################################

# This will write out the log sentence probabilities
#   log P(W) = sum_i P(w_n | w_1..w_n-1)
# of all test sentences in the form log P(W)=<value>, one line per test
# sentence.
#
# This is accomplished by writing out the value of the CE criterion, which
# is an aggregate over all words in a minibatch. By presenting each sentence
# as a separate minibatch, the CE criterion is equal to the log sentence prob.
#
# This can be used for N-best rescoring if you prepare your N-best hypotheses
# as an input file with one line of text per hypothesis, where the output is
# the corresponding log probabilities, one value per line, in the same order.

write = [
    action = "write"

    # last trained model
    # We need to make a change:
    BrainScriptNetworkBuilder = ([

        # import some names
        Previous = BS.Loop.Previous
        IsFirst = BS.Loop.IsFirst

        Splice (inputs, axis=1) =
            if axis == 1
            then RowStack (inputs)
            else [
                ArrayTransposeDimensions (inputs, axis1, axis2) = [
                    inputsT[i:0..Length(inputs)-1] = TransposeDimensions (inputs[i], axis1, axis2)
                ].inputsT
                out = TransposeDimensions (RowStack (ArrayTransposeDimensions (inputs, 1, axis)), 1, axis)
            ].out

        beamDepth = 2 // 0=predict; 1=greedy; >1=beam

        oldModel = BS.Network.Load ("$modelPath$")

        # replace old decoderFeedback node by newDecoderFeedback
        decoderInput1 = oldModel.decoderInput.RowStackArgs[1]
        newDecoderInput1 = BS.Loop.Previous (oldModel.decoderOutputEmbedded)
        # need a way to find a node by name, then this is easy
        # or a way to rename a node as an edit? finding and assigning is renaming
        # or a way to force-name nodes--Pass(, name='')? Can we explicitly name them?
        greedyDecodingModel = BS.Network.Edit (oldModel, BS.Network.Editing.ReplaceLinksToNode (decoderInput1, newDecoderInput1))

        # beam search of width 'beamDepth'
        beamDecodingModel = [
            # turning a regular LSTM to a top-N beam-search decoder:
            #  - add a depth axis of dimension N to all nodes inside the decoder loop
            #     - only needs the init signal for PastValue to be that
            #  - h and c must be shuffled versions of their PastValue
            #     - since what are the top N in one time step is not the top N in the next
            #     - reshufling and adding depth to the init signal can be done at the same place
            #  - decoder output must determine the top N and a reshuffling matrix for h and c
            #     - the current Hardmax needs to be replaced by something that outputs these (output depth N)
            #     - we get a N^2 depth: [V x (input set) x (top N output hypos)]
            #     - reshuffling matrix is reduction over V (multiply with row of V ones) plus possibly a transposition
            #  - we need an accumulated path score
            #     - start value constructed by stacking a 0 and N-1 -INF
            #  - for testing, we can output the current best in each step
            #     - that's a Slice()
            #  - traceback is a right-to-left recurrence
            #     - output best hypo conditioned on the path (it is already known)

            m1 = oldModel

            # this comes out of m1:
            #  decoder[0].prevState.h = PastValue (decoder[0].lstmState._privateInnards.ht) : [200 x 1 {1,200} x *] -> [200 x 1 {1,200} x *]
            #  decoder[0].prevState.c = PastValue (decoder[0].lstmState._privateInnards.ct) : [200 x 1 {1,200} x *] -> [200 x 1 {1,200} x *]
            #  decoderInput.inputs[1] = PastValue (labelsEmbedded) : [300 x 1 {1,300} x *] -> [300 x 1 {1,300} x *]

            hiddenDim    = m1.delayedDecoderFeedback.dim
            embeddingDim = m1.decoderOutputEmbedded.dim
            vocabSize    = m1.z.dim

            SkipFirst (x) = BS.Sequences.Skip (1, x)

            # replace every reference of these by PropagateTopN(of these)
            EditToPropagateTopN (name) = (node => if node.name == name then PropagateTopN (node) else node)
            propagationEdits[i:0..3] = // TODO: implement and use { } syntax
                if      i == 0 then EditToPropagateTopN ('decoder[0].prevState.h')
                else if i == 1 then EditToPropagateTopN ('decoder[0].prevState.c')
                else if i == 2 then BS.Network.Editing.ReplaceLinksToNamedNode ('z1.out', z1)
                else                BS.Network.Editing.ReplaceLinksToNode (m1.decoderInput.RowStackArgs[1], PastValue ((embeddingDim : beamDepth), m1.decoderOutputEmbedded))
                # note: The RowStack operation should broadcast the thoughtVector automatically

            z1 = SkipFirst (decode)

            m2 = BS.Network.Edit (m1, propagationEdits)

            # propagate LSTM state to the right top-N rank given where that rank came from in the previous time step
            PropagateTopN (h /* or c*/) = Previous (SampleTimes (h.PastValueArgs[0], backPointers))
                # backPointers: [Dprev, Dnew]
                #   v--------- best came from input hyp[1]
                #     v------- second best came from input hyp[0]
                #       v----- third best came from input hyp[2]
                #   0 1 0
                #   1 0 0
                #   0 0 1
                # backPointers[:,n] one-hot encodes the best predecessor at top-N rank n
                # each column is a one-hot vector
                # multiplying with such a column from the right will select the column represented by the one-hot value

            # get decoder log likelihoods
            logLLs = m1.z    # [V x D] un-normalized log P(w|hist) + const

            # decoder start token: 0 for first hyp, -INF for the others
            LOGZERO = -1e30
            initialPathScores = FirstAndOther (0, LOGZERO, beamDepth, axis = 2)  # row vector: [ 0, -INF, -INF, -INF, ... ]

            expandedPathScores = logLLs + PreviousOrDefault (pathScores, initialPathScores)        # [V x Dprev] un-normalized log (P(w|hist) * P(hist)) for all top D hypotheses
            # ^^ path expansion, [V x 1] + [1 x D] -> [V x D]

            # macro that extracts top D hypotheses
            # input: expandedPathScores[w,n]      w = word index, d = hyp index in beam (d=0 is the best one)
            # output: [w,n1,n2]     n1 = input hyp index (prev top N); n2 = output hyp index (new top N)
            # e.g. 4 words, beam 3; view this as 3 [4x3] planes "drawn" 3-dimensionally, with depth being the 3rd tensor index
            GetTopNTensor (D, expandedPathScores) = [
                # recurse over up to D elements
                # In each recursion:
                #  - pick the best over (w,n)
                #  - subtract it out from expandedPathScores
                recursion[n:0..D-1] =
                [
                    thisLogP = if n == 0                          # expandedPathScores excluding paths better than rank n
                               then expandedPathScores            # top: just the path scores
                               else recursion[n - 1].nextBestLogP # next: path scores after removing all we already got
                    best = Hardmax (thisLogP)                     # best = one-hot over (w,n)
                    nextBestLogP = thisLogP + Constant (LOGZERO) .* best      # the ones we've got get set to zero (represented as -INF since these are logs)
                ]
                # splice them together into a single tensor
                asArray[n:0..D-1] = recursion[n].best  # this is a BS array consisting only of the 'best' field    ('from r in recursion select r.best')
                spliced = Splice (axis = 3, asArray)   # convert BS array index n to tensor index n1
            ].spliced

            tokenSet = GetTopNTensor (beamDepth, expandedPathScores) # [V x Dprev] -> [V x Dprev x Dnew]
            #   +-----+
            #   |0 0 0|
            #   |0 0 0|-+
            #   |0 1 0|0|     means word[2] in input hyp[1] was the best
            #   |0 0 0|0|-+
            #   +-----+0|0|
            #     |1 0 0|0|   means word[3] in input hyp[0] was the second best
            #     +-----+1|   means word[2] in input hyp[2] was the third best
            #       |0 0 0|
            #       +-----+

            backPointers = Times (ConstantTensor (1, vocabSize), tokenSet, outputRank = 0) # this is a tensor Times operation that reduces over the first dimension
            # before dropping the first dimension: [V x Dprev x Dnew]
            #   +-----+
            #   |0 1 0|       means input hyp[1] gave rise to the best    
            #   +-----+-+  
            #     |1 0 0|     means input hyp[0] gave rise to second best
            #     +-----+-+
            #       |0 0 1|   means input hyp[2] gave rise to third best
            #       +-----+
            # after: [Dprev,Dnew]        e.g. "0 1 0" goes into first column, vertically
            #   v--------- best came from input hyp[1]
            #     v------- second best came from input hyp[0]
            #       v----- third best came from input hyp[2]
            #   0 1 0
            #   1 0 0
            #   0 0 1
            # backPointers[:,n] one-hot encodes the best predecessor at top-N rank n

            tokenSetScores = tokenSet .* expandedPathScores   # [V x Dprev x Dnew]
            #   +-----+
            #   |0 0 0|
            #   |0 0 0|-+
            #   |0 x 0|0|     x denotes the accumulated path score max_w P(w|hyp[1])
            #   |0 0 0|0|-+
            #   +-----+0|0|
            #     |y 0 0|0|   y denotes the accumulated path score max_w P(w|hyp[0])
            #     +-----+z|   z denotes the accumulated path score max_w P(w|hyp[2])
            #       |0 0 0|
            #       +-----+
            pathScores = ConstantTensor (1, (1/*output dim*/ : /*reduction dims: */vocabSize : beamDepth/*Dprev*/)) * tokenSet  # [1 x Dnew]

            # traceback
            # last state: take Hardmax over pathScores
            # previous states: multiply wth respective backPointers matrix
            # -> hyp index for every time step
            # then finally use that to select the actual output   TODO: That's a sample-wise matrix product between two sequences!!!
            traceback = NextOrDefault (backPointers * traceback, finalHyp)    # [D] one-hot, multiplying backPointers from the left will select another one-hot row of backPointers
            finalHyp = FirstAndOther (1, 0, beamDepth, axis = 1)              # the final token is the top-scoring hypothesis, that is, hyp[0]

            # and the actual decoding output
            # This is the one to output.
            decode = [
                hyp = Times (tokenSet, traceback, outputRank = 2)   # [V x Dprev] 2D one-hot
                out = hyp * ConstantTensor (1, beamDepth)           # reduces over Dprev -> 1D one-hot
            ].out
            # traceback : [Dnew]
            # tokenSet : [V x Dprev x Dnew]
            #   +-----+
            #   |0 0 0|
            #   |0 0 0|-+
            #   |0 1 0|0|     means word[2] in input hyp[1] was the best
            #   |0 0 0|0|-+
            #   +-----+0|0|
            #     |1 0 0|0|   means word[3] in input hyp[0] was the second best
            #     +-----+1|   means word[2] in input hyp[2] was the third best
            #       |0 0 0|
            #       +-----+

            z = decode;

            # helper macros  --> move to BS.core.bs

            # matrix product where A consists of MB samples, and B is multiplied from the right to every sample
            # Note: 'transposition' here is always sample-wise.  TODO: Times node should be able to do this by itself.
            SampleTimes (A, B) = Transpose (TransposeTimes (B, Transpose (A)))   # C = (B' * A')'

            FirstAndOther (firstVal, otherVals, N, axis = 1) = [
                axis1 = axis  # TODO: Is this really necessary? Why? Then we need the syntax   axis = ^.axis or ^axis
                out = if axis == 1
                      then RowStack (ConstantTensor (firstVal, 1) : ConstantTensor (otherVals, N -1))   # col vector: [ 1; 0; 0; 0 ... ]
                      else Splice (Constant (firstVal) : ConstantTensor (otherVals, (1 : N -1)), axis = axis1 /*, axis*/)   # row vector: [ 0, -INF, -INF, -INF, ... ]
            ].out

            PreviousOrDefault (x, initialValue) =   # a delay node with initial value
                BS.Boolean.If (BS.Loop.IsFirst (x),
                /*then*/ initialValue,
                /*else*/ BS.Loop.Previous (x))
                #if BS.Loop.IsFirst (x)
                #then initialValue
                #else BS.Loop.Previous (x)

            NextOrDefault (x, initialValue) =   # a delay node with initial value
                BS.Boolean.If (BS.Loop.IsLast (x),
                /*then*/ initialValue,
                /*else*/ BS.Loop.Next (x))
                #if BS.Loop.IsLast (x)
                #then initialValue
                #else BS.Loop.Next (x)
        ].m2

        model = if beamDepth == 0 then oldModel
           else if beamDepth == 1 then greedyDecodingModel
           else                          beamDecodingModel

    ].model)

    #outputPath = "$OutputDir$/Write"
    outputPath = "-"                    # "-" will write to stdout; useful for debugging
    #outputNodeNames = z1.out:labels1 # when processing one sentence per minibatch, this is the sentence posterior
    outputNodeNames = network.beamDecodingModel.z1.out:labels1 # when processing one sentence per minibatch, this is the sentence posterior
    format = [
        type = "category"
        transpose = false
        labelMappingFile = "$ModelDir$/vocab.wl"
        sequenceEpilogue = "\t// %s\n"
    ]

    minibatchSize = 8192                # choose this to be big enough for the longest sentence
    # need to be small since models are updated for each minibatch
    traceLevel = 1
    epochSize = 0

    reader = [
        file = "$DataDir$/$testFile$"
        #randomize = "none" # gets ignored
    
        # everything below here is duplicated from 'reader'
        readerType = LMSequenceReader
        mode = "softmax"
        nbruttsineachrecurrentiter = 1      # 1 means one per minibatch
        cacheBlockSize = 100000000          # read block size. This value is large enough to load entire corpus at once

        # word class info
        wordclass = "$ModelDir$/vocab.txt"

        #### write definition
        # if writerType is set, we will cache to a binary file
        # if the binary file exists, we will use it instead of parsing this file
        #writerType = BinaryReader
        wfile = $CacheDir$\sequenceSentence.bin
        # if calculated size would be bigger, that is used instead
        wsize = 256
        #wrecords - number of records we should allocate space for in the file
        # files cannot be expanded, so this should be large enough. If known modify this element in config before creating file
        wrecords = 1000
        #windowSize - number of records we should include in BinaryWriter window
        windowSize = 10000
    
        # additional features sections
        # For input labels, we need both 'features' and the first labels section (called 'inputLabelsDef' below)
        input = [
            dim = 0     # no (explicit) labels   ...labelDim correct??
            ### write definition
            sectionType = "data"
        ]
        # labels sections
        # TODO: seems we must specify two labels (in and out), but labelType = "none" is allowed
        # labels sections  --this is required, but our labels are extracted from the inLabels
        inputLabelsDef = [ # BUGBUG: Make sure that this section name comes before the dummy output labels alphabetically
            dim = 1

            # vocabulary size
            labelType = "category"
            labelDim = "$confVocabSize$"
            labelMappingFile = "$ModelDir$/vocab.wl"
            beginSequence = "</s>"
            endSequence   = "</s>"

            #### Write definition ####
            # sizeof(unsigned) which is the label index type
            elementSize=4
            sectionType=labels
            mapping = [
              #redefine number of records for this section, since we don't need to save it for each data record
              wrecords=11
              #variable size so use an average string size
              elementSize=10
              sectionType=labelMapping
            ]
            category = [
              dim=11
              #elementSize=sizeof(ElemType) is default
              sectionType=categoryLabels
            ]
        ]
        outputDummy = [
            labelType = "none"
        ]
    ]
]
